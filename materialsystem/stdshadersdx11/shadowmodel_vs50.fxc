//	DYNAMIC: "DOWATERFOG"			"0..1"
//	DYNAMIC: "SKINNING"				"0..1"

#include "common_vs_fxc.h"
#include "common_cbuffers_def_fxc.h"

static const bool g_bSkinning		= SKINNING ? true : false;
static const int  g_FogType			= DOWATERFOG;

cbuffer ShadowModel : register( b4 )
{
    const float4 cShadowTextureMatrix[3];
    const float4 cTexOrigin;
    const float4 cTexScale;
    const float4 cShadowConstants;
    const float4 cModulationColor;
}

#define flShadowFalloffOffset	cShadowConstants.x
#define flOneOverShadowDist		cShadowConstants.y
#define flShadowScale			cShadowConstants.z

struct VS_INPUT
{
	float4 vPos				: POSITION;
	float3 vNormal			: NORMAL;
	float4 vBoneWeights		: BLENDWEIGHT;
	float4 vBoneIndices		: BLENDINDICES;
};

struct VS_OUTPUT
{
    float4 projPos			: POSITION;
	float3 T0				: TEXCOORD0;
	float3 T1				: TEXCOORD1;
	float3 T2				: TEXCOORD2;
	float  T3				: TEXCOORD3;
	float4 vColor			: COLOR0;
	float  fog				: FOG;
};

VS_OUTPUT main( const VS_INPUT v )
{
    VS_OUTPUT o = (VS_OUTPUT)0;

    float3 worldNormal, worldPos;
	SkinPositionAndNormal(
		g_bSkinning,
		v.vPos, v.vNormal,
		v.vBoneWeights, v.vBoneIndices,
		cModel,
		worldPos, worldNormal );

    // Transform into projection space
    float4 projPos = mul( float4( worldPos, 1.0f ), cViewProj );
    o.projPos = projPos;

    //o.fog = CalcFog( worldPos, projPos, g_FogType );

    float3 vTexturePos;
	vTexturePos.x = dot( worldPos.xyz, cShadowTextureMatrix[0].xyz );
	vTexturePos.y = dot( worldPos.xyz, cShadowTextureMatrix[1].xyz );
	vTexturePos.z = dot( worldPos.xyz, cShadowTextureMatrix[2].xyz );

    float flShadowFade = ( vTexturePos.z - flShadowFalloffOffset ) * flOneOverShadowDist;

	// Offset it into the texture
	o.T0 = vTexturePos * cTexScale.xyz + cTexOrigin.xyz;

	// We're doing clipping by using texkill
	o.T1.xyz = vTexturePos.xyz;		// Also clips when shadow z < 0 !
	o.T2.xyz = float3( 1.0f, 1.0f, 1.0f ) - vTexturePos.xyz;
	o.T2.z = 1.0f - flShadowFade;	// Clips when shadow z > shadow distance	

	// We're doing backface culling by using texkill also (wow yucky)
	// --------------------------------------------------------------
	// Transform z component of normal in texture space
	// If it's negative, then don't draw the pixel
	o.T3 = dot( worldNormal, -cShadowTextureMatrix[2].xyz );

	// Shadow color, falloff
	o.vColor.xyz = cModulationColor.xyz;
	o.vColor.w = flShadowFade * flShadowScale;

    return o;
}